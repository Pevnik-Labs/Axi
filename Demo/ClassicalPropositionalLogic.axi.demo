// We declare some propositions that we will use in the rest of this file.
declaration P Q R : Prop

// The built-in way of classical reasoning is `by-contradiction (~ P) in e`,
// which proves `P` provided that `e` proves `False` in context extended with
// the assumption `~ P`.
// We can use `by-contradiction` to prove all the classical laws,
// like double negation elimination.
theorem double-negation : ~ ~ P --> P
proof
  assume (~ ~ P)
  by-contradiction (~ P)
  apply (~ ~ P) (~ P)
qed

// In proofterm style, the above proof looks as follows.
theorem double-negation-term-style : ~ ~ P --> P =
  assume (~ ~ P) in
    by-contradiction (~ P) in
      (apply (~ ~ P) (~ P))

// Law of Excluded Middle.
// Note that just like with `assume`, we can name assumptions introduced
// using `by-contradiction`.
theorem lem : P \/ ~ P
proof
  by-contradiction (not-lem = ~ (P \/ ~ P))
  apply not-lem
  or-right
  assume P
  apply not-lem
  or-left
  P
qed

// You might have already noticed that it's a frequent idiom to first
// use `by-contradiction (~ P)` and then `apply (~ P)` right
// away. This pattern of reasoning is called 'Consequentia Mirabilis'
// and can be encapsulated in a theorem.
theorem cm : (~ P --> P) --> P
proof
  assume (~ P --> P)
  by-contradiction (~ P)
  apply (~ P), (~ P --> P)
  assumption
qed

// Peirce's law.
theorem peirce : ((P --> Q) --> P) --> P
proof
  assume (pqp = _)
  by-contradiction (~ P)
  apply (~ P), pqp
  assume P
  contradiction (~ P) P
qed

// Of the two laws of contraposition, one is only valid classically.
theorem contraposition-classic : (~ Q --> ~ P) --> (P --> Q)
proof
  assume (nqnp = _) p
  by-contradiction (~ Q)
  apply nqnp (~ Q) P
qed

// There's also a funny classic version of contraposition.
theorem funny-contraposition-classic : (~ P --> Q) --> (~ Q --> P)
proof
  assume (~ P --> Q) (~ Q)
  by-contradiction (~ P)
  apply (~ Q), (~ P --> Q)
  assumption
qed

// In classical logic, if `P` implies `Q`, either `P` is false or `Q` is true.
theorem material-implication-intro : (P --> Q) --> ~ P \/ Q
proof
  assume (P --> Q)
  by-contradiction (no = ~ (~ P \/ Q))
  apply no
  or-left
  assume P
  apply no
  or-right
  apply (P --> Q)
  P
qed

// In classical logic, to prove a disjunction, when choosing which disjunct to
// prove, we can assume the other disjunct is false.
theorem weak-or-elim : (~ P --> Q) --> P \/ Q
proof
  assume (~ P --> Q)
  by-contradiction (npq = ~ (P \/ Q))
  apply npq
  or-right
  apply (~ P --> Q)
  assume P
  apply npq
  or-left
  P
qed

// In classical logic, to prove a conjunction it suffices to prove it's impossible
// that either conjunct is false.
theorem and-from-weak-and : ~ (~ P \/ ~ Q) --> P /\ Q
proof
  assume (pq = _)
  by-contradiction (both (~ P) (~ Q))
  apply pq
  or-left
  assumption
qed

// In classical logic, `P xor Q` follows from the fact that they aren't
// logically equivalent.
theorem xor-not-iff : ~ (P <--> Q) --> (P /\ ~ Q) \/ (~ P /\ Q)
proof
  assume (npq = _)
  cases (lem P) // illegal, lem not yet universally quantified
  - assume P
    or-left
    both P
    assume Q
    apply npq
    both
    - assume P
      assumption
    - assume Q
      assumption
  - assume (~ P)
    or-right
    both (~ P)
    by-contradiction (~ Q)
    apply npq
    both
    - assume P
      contradiction (~ P) P
    - assume Q
      contradiction (~ Q) Q
qed

// In classical logic, propositions are linearly ordered by implication.
theorem godel-dummet : (P --> Q) \/ (Q --> P)
proof
  by-contradiction (h = _)
  apply h
  or-left
  assume P
  by-contradiction (~ Q)
  apply h
  or-right
  assume Q
  contradiction (~ Q) Q
qed

// Premise independence for disjunction.
theorem premise-independence : (P --> Q \/ R) --> ((P --> Q) \/ (P --> R))
proof
  assume (pqr = _)
  by-contradiction (ng = _)
  apply ng
  or-left
  assume P
  by-contradiction (nq = ~ Q)
  apply ng
  cases (apply pqr P)
  - assume Q
    or-left
    assume _
    assumption
  - assume P
    or-right
    assume _
    assumption
qed