// We declare some propositions that we will use in the rest of this file.
declaration P Q R : Prop

// The built-in way of classical reasoning is `by-contradiction (~ P) in e`,
// which proves `P` provided that `e` proves `False` in context extended with
// the assumption `~ P`.
// We can use `by-contradiction` to prove all the classical laws,
// like double negation elimination.
theorem double-negation : ~ ~ P --> P :=
  assume (~ ~ P)
  by-contradiction (~ P)
  modus-ponens (~ ~ P) (~ P)

// In proofterm style, the above proof looks as follows.
theorem double-negation-term : ~ ~ P --> P :=
  assume (~ ~ P) in
    by-contradiction (~ P) in
      (modus-ponens (~ ~ P) (~ P))

// Law of Excluded Middle.
// Note that just like with `assume`, we can name assumptions introduced
// using `by-contradiction`.
theorem lem : P \/ ~ P :=
  by-contradiction (not-lem := ~ (P \/ ~ P))
  modus-ponens not-lem
  or-right
  assume P
  modus-ponens not-lem
  or-left
  P

// You might have already noticed that it's a frequent idiom to first
// use `by-contradiction (~ P)` and then `modus-ponens (~ P)` right
// away. This pattern of reasoning is called 'Consequentia Mirabilis'
// and can be encapsulated in a theorem.
theorem cm : (~ P --> P) --> P :=
  assume (~ P --> P)
  by-contradiction (~ P)
  modus-ponens (~ P), (~ P --> P)
  assumption

// Peirce's law.
theorem peirce : ((P --> Q) --> P) --> P :=
  assume (pqp := _)
  by-contradiction (~ P)
  modus-ponens (~ P), pqp
  assume P
  contradiction (~ P) P

// Of the two laws of contraposition, one is only valid classically.
theorem contraposition-classic : (~ Q --> ~ P) --> (P --> Q) :=
  assume (nqnp := _) p
  by-contradiction (~ Q)
  modus-ponens nqnp (~ Q) P

// There's also a funny classic version of contraposition.
theorem funny-contraposition-classic : (~ P --> Q) --> (~ Q --> P) :=
  assume (~ P --> Q) (~ Q)
  by-contradiction (~ P)
  modus-ponens (~ Q), (~ P --> Q)
  assumption

// In classical logic, if `P` implies `Q`, either `P` is false or `Q` is true.
theorem material-implication-intro : (P --> Q) --> ~ P \/ Q :=
  assume (P --> Q)
  by-contradiction (no := ~ (~ P \/ Q))
  modus-ponens no
  or-left
  assume P
  modus-ponens no
  or-right
  modus-ponens (P --> Q)
  P

// In classical logic, to prove a disjunction, when choosing which disjunct to
// prove, we can assume the other disjunct is false.
theorem weak-or-elim : (~ P --> Q) --> P \/ Q :=
  assume (~ P --> Q)
  by-contradiction (npq := ~ (P \/ Q))
  modus-ponens npq
  or-right
  modus-ponens (~ P --> Q)
  assume P
  modus-ponens npq
  or-left
  P

// In classical logic, to prove a conjunction it suffices to prove it's impossible
// that either conjunct is false.
theorem and-from-weak-and : ~ (~ P \/ ~ Q) --> P /\ Q :=
  assume (pq := _)
  by-contradiction (both (~ P) (~ Q))
  modus-ponens pq
  or-left
  assumption

// In classical logic, `P xor Q` follows from the fact that they aren't
// logically equivalent.
theorem xor-not-iff : ~ (P <--> Q) --> (P /\ ~ Q) \/ (~ P /\ Q) :=
  assume (npq := _)
  or-elim (lem P) // illegal, lem not yet universally quantified
  - assume P
    or-left
    both P
    assume Q
    modus-ponens npq
    both
    - assume P
      assumption
    - assume Q
      assumption
  - assume (~ P)
    or-right
    both (~ P)
    by-contradiction (~ Q)
    modus-ponens npq
    both
    - assume P
      contradiction (~ P) P
    - assume Q
      contradiction (~ Q) Q

// In classical logic, propositions are linearly ordered by implication.
theorem godel-dummet : (P --> Q) \/ (Q --> P) :=
  by-contradiction (h := _)
  modus-ponens h
  or-left
  assume P
  by-contradiction (~ Q)
  modus-ponens h
  or-right
  assume Q
  contradiction (~ Q) Q

// Premise independence for disjunction.
theorem premise-independence : (P --> Q \/ R) --> ((P --> Q) \/ (P --> R)) :=
  assume (pqr := _)
  by-contradiction (ng := _)
  modus-ponens ng
  or-left
  assume P
  by-contradiction (nq := ~ Q)
  modus-ponens ng
  or-elim (modus-ponens pqr P)
  - assume Q
    or-left
    assume _
    assumption
  - assume P
    or-right
    assume _
    assumption