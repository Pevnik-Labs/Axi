// We declare some propositions that we will use in the rest of this file.
declaration P Q R : Prop

// The built-in way of classical reasoning is `by-contradiction x : ~ P in e`,
// which proves `P` provided that `e` proves `False` in context extended with
// the assumption `x : ~ P`.
// We can use `by-contradiction` to prove all the classical laws,
// like double negation elimination.
theorem double-negation : ~ ~ P --> P
proof
  assume nnp : ~ ~ P
  by-contradiction np : ~ P
  apply nnp np
qed

// In proofterm style, the above proof looks as follows.
theorem double-negation-term-style : ~ ~ P --> P =
  assume nnp : ~ ~ P in
    by-contradiction np : ~ P in
      apply nnp np

// Note that just like for assume, we can omit the annotation.
theorem double-negation-no-annotation : ~ ~ P --> P
proof
  assume nnp
  by-contradiction np
  apply nnp np
qed

// Law of Excluded Middle.
theorem lem : P \/ ~ P
proof
  by-contradiction not-lem : ~ (P \/ ~ P)
  apply not-lem
  or-right
  assume p
  apply not-lem
  or-left
  p
qed

// You might have already noticed that it's a frequent idiom to first
// use `by-contradiction` and then `apply` right away. This pattern of
// reasoning is called "Consequentia Mirabilis" and can be encapsulated
// in a theorem.
theorem cm : (~ P --> P) --> P
proof
  assume npp : ~ P --> P
  by-contradiction np : ~ P
  apply np, npp
  assumption
qed

// Peirce's law.
theorem peirce : ((P --> Q) --> P) --> P
proof
  assume pqp : (P --> Q) --> P
  by-contradiction np : ~ P
  apply np, pqp
  assume p
  absurd
  apply contradiction np p
qed

// Of the two laws of contraposition, this one is only valid classically.
theorem contraposition-classic : (~ Q --> ~ P) --> (P --> Q)
proof
  assume nqnp p
  by-contradiction nq
  apply nqnp nq p
qed

// There's also a funny classic version of contraposition.
theorem funny-contraposition-classic : (~ P --> Q) --> (~ Q --> P)
proof
  assume (npq : ~ P --> Q) (nq : ~ Q)
  by-contradiction np : ~ P
  apply nq, npq
  assumption
qed

// In classical logic, if `P` implies `Q`, either `P` is false or `Q` is true.
theorem material-implication-intro : (P --> Q) --> ~ P \/ Q
proof
  assume pq : P --> Q
  by-contradiction no : ~ (~ P \/ Q)
  apply no
  or-left
  assume p
  apply no
  or-right
  apply pq
  p
qed

// In classical logic, when choosing which disjunct to prove,
// we can assume the other disjunct is false.
theorem weak-or-elim : (~ P --> Q) --> P \/ Q
proof
  assume npq : ~ P --> Q
  by-contradiction npq : ~ (P \/ Q)
  apply npq
  or-right
  apply npq
  assume p
  apply npq
  or-left
  p
qed

// In classical logic, to prove a conjunction it suffices to prove it's
// impossible that either conjunct is false.
theorem and-from-weak-and : ~ (~ P \/ ~ Q) --> P /\ Q
proof
  assume pq : ~ (~ P \/ ~ Q)
  by-contradiction npq : ~ (P /\ Q)
  apply pq
  or-left
  assume p : P
  apply pq
  or-right
  assume q : Q
  apply npq
  both p q
qed

// In classical logic, `P xor Q` follows from the fact that they aren't
// logically equivalent.
theorem xor-not-iff : ~ (P <--> Q) --> (P /\ ~ Q) \/ (~ P /\ Q)
proof
  assume npq : ~ (P <--> Q)
  cases (lem P)  // This is slightly illegal, because for now `lem` is only about `P`, so the syntax should be `cases lem`.
  - assume p
    or-left
    both p
    assume q
    apply npq
    both
    - assume _
      assumption
    - assume _
      assumption
  - assume np : ~ P
    or-right
    both np
    by-contradiction nq : ~ Q
    apply npq
    both
    - assume p
      absurd
      apply contradiction np p
    - assume q
      absurd
      apply contradiction nq q
qed

// In classical logic, propositions are linearly ordered by implication.
theorem godel-dummet : (P --> Q) \/ (Q --> P)
proof
  by-contradiction h
  apply h
  or-left
  assume _
  by-contradiction nq : ~ Q
  apply h
  or-right
  assume q
  absurd
  apply contradiction nq q
qed

// Premise independence for disjunction.
theorem premise-independence : (P --> Q \/ R) --> (P --> Q) \/ (P --> R)
proof
  assume pqr : P --> Q \/ R
  by-contradiction no : ~ ((P --> Q) \/ (P --> R))
  apply no
  or-left
  assume p
  absurd
  apply no
  cases (apply pqr p)
  - assume q
    or-left
    assume _
    assumption
  - assume r
    or-right
    assume _
    assumption
qed