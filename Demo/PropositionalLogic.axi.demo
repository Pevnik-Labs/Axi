// The syntax of propositions is as follows:
// P, Q ::= True | False | P --> Q | P /\ Q | P \/ Q | P <--> Q

// We declare some propositions that we will use in the rest of this file.
declaration P Q R : Prop

// Implication is written `-->`.
// The implication `P --> Q` is proved using `assume P in q`,
// where `q` is a proof of `Q`.
// Note that we claim the truth of `P` by writing just `P`, as in DPLs/Athena
// and contrary to type theoretical systems, where we would refer to
// something like `p : P`, a proof of P, instead.
// Note that theorem names can contain '-'.
theorem impl-refl : P --> P :=
  assume P in P

// The above proof was written in proofterm style, like in DPLs/Athena,
// but we can also write proofs in tactic style, like in Coq or Lean.
// Moreover, the tactic style directly corresponds to the proofterm style,
// unlike in Coq or Lean. The mechanism is as follows: if we have a proofterm
// that takes one argument, like `proof arg`, we can instead write it as a block
// ```
// proof
// arg
// ```
// For proofterms that bind variables, we can omit the `in`, as in the
// example below. Note that the proofterm doesn't need to be unary - it
// just needs to be applied enough so that only the last argument is missing.
theorem weakening : P --> Q --> P :=
  assume P
  assume Q
  P

// When two or more `assume`s follow each other, we can condense them into one.
theorem assume-chaining : P --> Q --> P :=
  assume P Q
  P

// When we need to introduce an assumption that won't be used, we can discard
// it by writing an underscore.
theorem assume-discard : P --> P --> P :=
  assume P _
  P

// We can name assumptions inline with the syntax `assume (name = prop)`.
theorem assume-naming : P --> Q --> P :=
  assume (x = P) (y = Q)
  x

// If the propositions are really long to write down, we can omit them and
// mention just their new names.
theorem assume-only-naming : P --> Q --> P :=
  assume (x = _) (y = _)
  x

// If we name all assumptions without writing them out in full, the syntax
// can get a bit shorter.
theorem assume-only-naming-short : P --> Q --> P :=
  assume (x y = _)
  x

// It would be optimal if we could completely avoid the `= _` part, but I'm not
// sure if that will be possible.
theorem assume-only-naming-shortest : P --> Q --> :=
  assume x y
  x

// If we want to avoid writing `assume` alltogether, we can put the premises
// before the final colon.
theorem no-assume P Q : P :=
  P

// When premises are to the left, we can still name them.
theorem no-assume-naming (x = P) (y = Q) : P :=
  x

// If we don't want to bother with recalling assumption names, we can say
// `assumption` instead.
theorem assumption-example : P --> Q --> P :=
  assume P Q
  assumption

// Implication elimination is `modus-ponens`.
theorem impl-elim : (P --> Q) --> P --> Q :=
  assume (P --> Q) P
  modus-ponens (P --> Q) P

// We can also use `modus-ponens` with more arguments
// when the implication is longer.
theorem modus-ponens-many-args : (P --> Q --> R) --> (Q --> P --> R) :=
  assume (P --> Q --> R) Q P
  modus-ponens (P --> Q --> R) Q P

// Just like for `assume`, `modus-ponens` can also be written in tactic style.
theorem impl-trans : (P --> Q) --> (Q --> R) --> (P --> R) :=
  assume (P --> Q) (Q --> R) P
  modus-ponens (Q --> R)
  modus-ponens (P --> Q)
  P

// When we need to use `modus-ponens` a few times in a row, we can condense it
// into a single line, with arguments separated by commas.
theorem modus-ponens-chaining : (P --> Q) --> (Q --> R) --> (P --> R) :=
  assume (P --> Q) (Q --> R) P
  modus-ponens (Q --> R), (P --> Q)
  P

// We can chain many-argument `modus-ponens` just like the ordinary one.
theorem impl-dist : (P --> Q --> R) --> ((P --> Q) --> (P --> R)) :=
  assume (pqr = P --> Q --> R) (pq = P --> Q) P
  modus-ponens pqr P, pq
  P

// The true proposition is written `True`.
// We can prove `True` with `true`.
// There's no `True` elimination, because it's useless.
theorem trivial : True :=
  true

// The false proposition is written `False`.
// `False` elimination is `exfalso`.
// There's no `False` introduction.
theorem also-trivial : False --> P :=
  assume False
  exfalso False

// The negation of `P` is written `~ P`.
// `~ P` is not a primitive, but rather an abbreviation of `P --> False`.
// Negation introduction is therefore `assume`, just as for implication.
theorem not-false : ~ False :=
  assume False
  False

// Negation elimination is `modus-ponens`.
// The below theorem, called `absurd`, may also be used as an eliminator for
// negation for the purpose of readability.
theorem absurd : ~ P --> P --> False :=
  assume (~ P) P
  modus-ponens (~ P) P

// The conjunction of `P` and `Q` is written `P /\ Q`.
// Conjunction introduction is written `both`.
theorem and-intro : P --> Q --> P /\ Q :=
  assume P Q
  both P Q

// Proofterms that are missing more than one argument can also be turned into
// tactics, but the syntax is a bit different: we need to enclose the tactic
// block for each subgoal in curly braces.
theorem tactic-style-two-args-example : P --> Q --> P /\ Q :=
  assume P Q
  both
  {
    P
  }
  {
    Q
  }

// Alternatively, we can use "-" as separators. These are called "bullets",
// following Coq naming.
theorem bullets-example : P --> Q --> P /\ Q :=
  assume P Q
  both
  - P
  - Q

// Missing arguments can also be named and then they are provided as blocks
// labeled with the corresponding names.
theorem named-blocks-example : P --> Q --> P /\ Q :=
  assume P Q
  both ?p ?q
  ?p: P
  ?q: Q

// I'm not yet sure what the best syntax for bullets is.
theorem alternative-named-bullets-example : P --> Q --> P /\ Q :=
  assume P Q
  both ?p ?q
  case ?p
    P
  case ?q
    Q

// Conjunction elimination is `and-elim-l` and `and-elim-r`.
theorem conjunction-elimination-example : P /\ Q --> P :=
  assume (P /\ Q)
  and-elim-l (P /\ Q)

// We can also eliminate conjunction by pattern matching directly in `assume`.
theorem assume-pattern-matching : P /\ Q --> P :=
  assume (both P Q)
  P

// This pattern-matching elimination can be nested, of course.
// Also note that we can use partially applied `both` in tactic style.
theorem and-assoc : (P /\ Q) /\ R --> P /\ (Q /\ R) :=
  assume (both (both P Q) R)
  both P
  both Q R

// The biconditional is written `P <--> Q`.
// `P <--> Q` is not a primitive, but an abbreviation of `(P --> Q) /\ (Q --> P)`.
// Therefore, biconditional introduction is `both`, just as for conjunction.
theorem iff-intro : (P --> Q) --> (Q --> P) --> P <--> Q :=
  assume (P --> Q) (Q --> P)
  both (P --> Q) (Q --> P)

// Biconditional elimination is `and-elim-l` and `and-elim-r`, just as for
// conjunction.
theorem iff-elim-l : (P <--> Q) --> (P --> Q) :=
  assume (P <--> Q)
  and-elim-l (P <--> Q)

// Similarly to conjunction, biconditionals can be eliminated by pattern
// matching directly in `assume`.
theorem iff-elim-r-pattern-matching : (P <--> Q) --> (Q --> P) :=
  assume (both (P --> Q) (Q --> P))
  Q --> P

// The disjunction of `P` and `Q` is written `P \/ Q`.
// Disjunction introduction is written `or-left`.
theorem or-intro-l : P --> P \/ Q :=
  assume P
  or-left P

// Disjunction elimination is written `or-elim`.
theorem or-idempotent : P \/ P --> P :=
  assume (P \/ P)
  or-elim (P \/ P) (assume P in P) (assume P in P)

// Note: we don't have to prove `P --> P` inline with `assume P in P`,
// because we can use `impl-refl`, a theorem we already proved.
// Also note that theorem names can contain an apostrophe.
theorem or-idempotent' : P \/ P --> P :=
  assume (P \/ P)
  or-elim (P \/ P) impl-refl impl-refl

// Note that similarly to `both`, we can use `or-elim` partially
// applied in tactic style.
theorem or-elim-tactic-style : P \/ Q --> Q \/ P :=
  assume (P \/ Q)
  or-elim (P \/ Q)
  {
    assume P
    or-right P
  }
  {
    assume Q
    or-left Q
  }

// The built-in way of classical reasoning is `by-contradiction`.
// ~ P |- e : False
// ----------------
// |- by-contradiction e : P
// We can use `by-contradiction` to prove double negation elimination.
theorem double-negation : ~ ~ P --> P :=
  assume (~ ~ P)
  by-contradiction
  modus-ponens (~ ~ P) (~ P)

// We can prove a lemma inline with the construct `follows P from p in q`,
// where `P` is a proposition, `p` is its proof and `q` is the proof of
// the main theorem.
// Formally, this is called a 'cut'.
theorem cut-example : P \/ P --> P :=
  assume (P \/ P)
  follows (P --> P) from
    assume P
    P
  or-elim (P \/ P) (P --> P) (P --> P)

// We can also name the lemma.
theorem named-cut-example : P \/ P --> P :=
  assume (P \/ P)
  follows (lemma = P --> P) from
    assume P
    P
  or-elim (P \/ P) lemma lemma

// Some exercises.

theorem lem : P \/ ~ P :=
  double-negation
  assume (not-lem = ~ (P \/ ~ P))
  modus-ponens not-lem
  or-right
  assume P
  modus-ponens not-lem
  or-left
  P

theorem curry : (P /\ Q --> R) --> P --> Q --> R :=
  assume (P /\ Q --> R) P Q
  modus-ponens (P /\ Q --> R)
  both P Q

theorem uncurry : (P --> Q --> R) --> (P /\ Q --> R) :=
  assume (P --> Q --> R) (both P Q)
  modus-ponens (P --> Q --> R) P Q

theorem or-comm : P \/ Q --> Q \/ P :=
  assume (P \/ Q)
  or-elim (P \/ Q) ?l ?r
  case ?l
    assume P
    or-right P
  case ?r
    assume Q
    or-left Q

/* Coq version:
Lemma or_comm_impl : P \/ Q -> Q \/ P.
Proof.
  intro.
  destruct H as [p | q].
    right. assumption.
    left. assumption.
Qed.
*/

theorem and-comm : P /\ Q --> Q /\ P :=
  assume (both P Q)
  both Q P

theorem impl-or-neg : ~ P \/ Q --> (P --> Q) :=
  assume (~ P \/ Q) P
  or-elim (~ P \/ Q)
  - modus-ponens (~ P) P
  - assumption

theorem noncontradiction : ~ (P /\ ~ P) :=
  assume (both P (~ P))
  modus-ponens (~ P) P

theorem contraposition : (P --> Q) --> (~ Q --> ~ P) :=
  assume (P --> Q) (~ Q) P
  modus-ponens (~ Q), (P --> Q)
  assumption

theorem iff-not : (P <--> Q) --> (~ P <--> ~ Q) :=
  assume (both (P --> Q) (Q --> P))
  both
  - modus-ponens contraposition (Q --> P)
  - modus-ponens contraposition (P --> Q)

theorem iff-elim : ((P --> Q) --> (Q --> P) --> R) --> ((P <--> Q) --> R) :=
  assume (f = _) (both (P --> Q) (Q --> P))
  modus-ponens f (P --> Q) (Q --> P)

theorem resolution : (P \/ Q) /\ (~ P \/ R) --> Q \/ R :=
  assume (both (P \/ Q) (~ P \/ R))
  or-elim (P \/ Q) ?p ?q
  case ?q
    assume Q
    or-left Q
  case ?p
    or-elim (~ P \/ R)
    - assume (~ P)
      absurd (~ P) P
    - assume R
      or-right R

theorem resolution' : (P \/ Q) /\ (~ P \/ R) --> Q \/ R :=
  assume (both (P \/ Q) (~ P \/ R))
  or-elim (P \/ Q) (assume P in ?p) (assume Q in ?q)
  case ?q
    or-left Q
  case ?p
    or-elim (~ P \/ R)
    - assume (~ P)
      absurd (~ P) P
    - assume R
      or-right R

/* Coq version:
Proof.
  intros [pq npr].
  destruct pq as [p | q]; cycle 1.
  - left.
    assumption.
  - destruct npr as [np | r].
    + contradiction.
    + right.
      assumption.
Qed.
/*

theorem funny-contraposition : (P --> ~ Q) --> (Q --> ~ P) :=
  assume (P --> ~ Q) Q P
  modus-ponens (P --> ~ Q) P Q

theorem not_or : ~ (P \/ Q) --> ~ P /\ ~ Q :=
  assume (npq = ~ (P \/ Q))
  both
  - assume P
    modus-ponens npq
    or-left
    P
  - assume Q
    modus-ponens npq
    or-right
    Q